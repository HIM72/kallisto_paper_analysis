import os

ANNO_PRE = "Homo_sapiens.GRCh37.75.cdna.pc"
ANNO_FA = "annotation/" + ANNO_PRE + ".fa"

KAL_IDX = "index/" + ANNO_PRE + ".kidx"

GEUVADIS_SAMPLES = os.listdir("data")

rule all:
    input:
        # generate the kallisto index
        KAL_IDX,
        # run kallisto on all GEUVADIS samples
        expand("analysis/kallisto/{sample}/abundance.h5",
            sample = GEUVADIS_SAMPLES)

# generate the kallisto index
rule kallisto_index:
    input:
        ANNO_FA
    output:
        KAL_IDX
    threads: 1
    shell:
        "kallisto index -f {input} -i {output}"

# run kallisto and output abundance estimates
rule kallisto:
    input:
        "data/{sample}/{sample}_1.fastq.gz",
        "data/{sample}/{sample}_2.fastq.gz",
        KAL_IDX
    output:
        "analysis/kallisto/{sample}/abundance.h5" 
    threads: 1
    shell:
        "kallisto quant "
        "-i {KAL_IDX} "
        "-b 100 "
        "-o analysis/kallisto/{wildcards.sample} "
        "{input[0]} {input[1]}"
